@doc raw"
Generates observations from an observation error model. It provides support for missing values in observations (`y_t`), and expected observations (`Y_t`) that are shorter than observations. When this is the case it assumes that the
expected observations are the last `length(Y_t)` elements of `y_t`.
It also pads the expected observations with a small value (1e-6) to mitigate potential numerical issues.

It dispatches to the `observation_error` function to generate the observation error distribution which uses priors generated by `generate_observation_error_priors` submodel. For most observation error models specific implementations of `observation_error` and `generate_observation_error_priors` are required but a specific implementation of `generate_observations` is not required.
"
@model function EpiAwareBase.generate_observations(
        obs_model::AbstractTuringObservationErrorModel,
        y_t,
        Y_t)
    @submodel priors = generate_observation_error_priors(obs_model, y_t, Y_t)

    if ismissing(y_t)
        y_t = Vector{Missing}(missing, length(Y_t))
    end

    diff_t = length(y_t) - length(Y_t)
    @assert diff_t>=0 "The observation vector must be longer than or equal to the expected observation vector"

    pad_Y_t = Y_t .+ 1e-6

    for i in eachindex(Y_t)
        y_t[i + diff_t] ~ observation_error(obs_model, pad_Y_t[i], priors...)
    end

    return y_t
end

@doc raw"
Generates priors for the observation error model. This should return a named tuple containing the priors required for generating the observation error distribution.
"
@model function generate_observation_error_priors(
        obs_model::AbstractTuringObservationErrorModel, y_t, Y_t)
    return NamedTuple()
end

@doc raw"
The observation error distribution for the observation error model. This function should return the distribution for the observation error given the expected observation value `Y_t` and the priors generated by `generate_observation_error_priors`.
"
function observation_error(obs_model::AbstractTuringObservationErrorModel, Y_t)
    @info "No concrete implementation for `observation_error` is defined."
    return nothing
end
